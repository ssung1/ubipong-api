plugins {
    // need Spring plugin because we want to use the dependency management
    id 'org.springframework.boot' version "${springBootVersion}"
    // we want to use dependency management so we don't need to know the versions of dependencies
    id 'io.spring.dependency-management' version "${springDependencyVersion}"
    id 'java-library'
    // automatically creates getters, setters, and other boilerplate
    id 'io.freefair.lombok' version "${lombokVersion}"
}

sourceCompatibility = 11

// need to specify this so Spring does not try to look for a main class
bootJar {
    enabled = false
}
// need to specify this so Spring does not try to look for a main class
jar {
    enabled = true
}
repositories {
    mavenCentral()
    jcenter()
}

// exposes test data to combined report
configurations {
    binaryTestResultsElements {
        canBeResolved = false
        canBeConsumed = true
        attributes {
            attribute(Category.CATEGORY_ATTRIBUTE, objects.named(Category, Category.DOCUMENTATION))
            attribute(DocsType.DOCS_TYPE_ATTRIBUTE, objects.named(DocsType, 'test-report-data'))
        }
        outgoing.artifact(test.binaryResultsDirectory)
    }
}

dependencies {
    // need Jackson for the annotations such as "JsonIgnoreProperties"
    implementation 'com.fasterxml.jackson.core:jackson-databind'

    // test stuff using spring to manage dependencies
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

test {
    // disable html report because we want to view the combined report instead
    reports.html.enabled = false
    // for junit report, we simply save them to a shared path
    testResultsDirName = "${rootProject.buildDir}/test-results/${project.name}"
    useJUnitPlatform()
}

